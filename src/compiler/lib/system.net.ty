function init(){
    system.loadLibrary("system_net".buffer,{[
        "system_net_ip2longNative".buffer,
        "system_net_createServerSocket".buffer,
        "system_net_listen".buffer,
        "system_net_accept".buffer,
        "system_net_send".buffer,
        "system_net_read".buffer,
        "system_net_getSocketAddress".buffer,
        "system_net_getSocketPort".buffer,
        "system_net_getSocketFamily".buffer
    ]});
};
class IPAddress{
    var host:long;
    var port:short;
    var family:short;
    var _point:object;//原始socket
    IPAddress(host:long,port:short,family:short){
        this.host=host;
        this.port=port;
        this.family=family;
    }
    function toString():string{
        return "ip:"+host.toString()+",port:"+port.toString();
    };
}
class Socket{
    var socket:object;
    var address:IPAddress;
    Socket(address:IPAddress){
        this.address=address;
    }
    Socket(s:object){
        socket=s;
    }
    function sendString(msg:string):int{
        return send(msg.buffer);
    };
    function send(buf:byte[]):int{
        return system.net.send(socket,buf);
    };
    function read(buf:byte[]):int{
        return system.net.read(socket,buf);
    };
}
class ServerSocket{
    var address:long;
    var port:int;
    var _socket:object;
    ServerSocket(host:string,port:int){
        address=ip2long(host);
        if(address==0l){
            throw "无效的ip地址:"+host;
        }
        this.port=port;
        _socket=createServerSocket(address,port);
        if(_socket==null){
            throw "创建socket失败";
        }
    }
    function listen(len:int){
        var ret=system.net.listen(_socket,len);
        if(ret<0){
            throw "listen失败";
        }
    };
    function accept():Socket{
        var fd=system.net.accept(_socket);
        if(fd==null){
            throw "accept失败";
        }
        var clientSocket = new Socket(fd);
        var ipAddress=new IPAddress(
            getSocketAddress(fd),
            getSocketPort(fd),
            getSocketFamily(fd)
        );
        clientSocket.address=ipAddress;
        return clientSocket;
    };
}

function accept(_socket:object):object{
    native
};

function listen(_socket:object,len:int):int{
    native
};

function send(_socket:object,buffer:byte[]):int{
    native
};

function read(_socket:object,buffer:byte[]):int{
    native
};

function createServerSocket(address:long,port:int):object{
    native
};

function getSocketAddress(_socket:object):long{
    native
};

function getSocketPort(_socket:object):short{
    native
};

function getSocketFamily(_socket:object):short{
    native
};

function ip2long(host:string):long{
    return ip2longNative(host.buffer);
};

//实在懒得写解析ip的代码了，用c++的算了
function ip2longNative(buffer:byte[]):long{
    native
};